---
layout: post
title: "[오태경의 머신러닝] 5일 단기 5일차 (2)"
excerpt: "오차 역전파 - 효율적인 편미분"
categories:
- Excel
tags:
- Excel
- 오태경의 머신러닝
- 5일 단기
last_modified_at: 2021-08-07T02:18:00+09:00
comments : true
---
<hr>

<h2>- Intro</h2>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드9.PNG">
</div>

<br>
<h2>- 신경망의 순전파</h2>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드10.PNG">
</div>

> 신경망이 출력 노드까지 계산되는 과정을 순전파라고 한다.

<br>
<h2>- 신경망의 오차 역전파</h2>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드11.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드12.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드13.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드14.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드15.PNG">
</div>

> 미분에 익숙하다면 연쇄법칙을 차근차근 적용해보며 혼자서 유도해보도록 하고, 식을 이해하는 것이 어렵다면 뒤에 나올 예제 코드를 먼저 훑어본 후 델타식을 암기하고 나서 유도해보는 것도 나쁘지 않다. 

<br>
<h2>- 예제 1</h2>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드16.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드17.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드18.PNG">
</div>

{% highlight vb %}
'변수 선언
Const L_rate = 1

Dim x(1 To 2) As Double
Dim w(1 To 4, 1 To 2, 1 To 3) As Double
Dim z(1 To 4, 1 To 2) As Double
Dim h(0 To 4, 1 To 2) As Double
Dim delta(1 To 4, 1 To 2) As Double

'LeaKy ReLU 정의
Public Function ReLU(ReLU_input)
If ReLU_input >= 0 Then
    ReLU = ReLU_input
Else
    ReLU = 0.1 * ReLU_input
End If
End Function

'LeaKy ReLU 편미분 정의
Public Function dReLU(dReLU_input)
If dReLU_input >= 0 Then
    dReLU = 1
Else
    dReLU = 0.1
End If
End Function

'Sigmoid 정의, 오버플로 방지
Public Function sigmoid(sigmoid_input)
If sigmoid_input < -700 Then
    sigmoid = 0
Else
    sigmoid = 1 / (1 + Exp(-sigmoid_input))
End If
End Function

'가중치 초기화 버튼 코드
'다른 시트에 가중치 랜덤 초기화
Sub 가중치초기화()
For l = 1 To 4
For n = 1 To 2
For m = 1 To 3
If (l = 4 And n = 2) = False Then
    Worksheets("weightbias").Cells(n + (l - 1) * 2, m).Value = (2 * Rnd - 1) / 3
End If
Next m
Next n
Next l
End Sub

'DNN 버튼 코드
Sub DNN()

'다른 시트로부터 가중치 불러오기
For l = 1 To 4 '신경망 층 수
For n = 1 To 2 '층 당 노드 수
For m = 1 To 3 '층 당 노드 수 + 1 (바이어스가 하나 추가)
If (l = 4 And n = 2) = False Then '(마지막 층에는 노드가 1개이다.)
    w(l, n, m) = Worksheets("weightbias").Cells(n + (l - 1) * 2, m).Value
End If
Next m
Next n
Next l

'2000번 반복 학습
For k = 1 To 2000
For i = 2 To 39

    '아래의 반복문의 편의를 위해 0번째 은닉층을 정의하고 x를 대입한다.
    x(1) = Cells(i, 1).Value
    x(2) = Cells(i, 2).Value
    h(0, 1) = x(1)
    h(0, 2) = x(2)
    
    For l = 1 To 4
    For n = 1 To 2
    If (l = 4) = False Then
        z(l, n) = 0
        For m = 1 To 2
            z(l, n) = z(l, n) + w(l, n, m) * h(l - 1, m)
        Next m
        z(l, n) = z(l, n) + w(l, n, 3)
        h(l, n) = ReLU(z(l, n)) '순전파 후 활성화 함수 Leaky ReLU
    End If
    Next n
    Next l
    
    z(4, 1) = w(4, 1, 1) * h(3, 1) + w(4, 1, 2) * h(3, 2) + w(4, 1, 3)
    h(4, 1) = sigmoid(z(4, 1)) 'sigmoid로 출력단에서 확률 생성
    y = h(4, 1)
    Cells(i, 4) = y
    
    ans = Cells(i, 3).Value
    
    delta(4, 1) = (y - ans) 'CEE와 sigmoid의 미분
    
    '출력레이어부터 시작하여 델타식 역전파
    For l = 1 To 3
    For m = 1 To 2
    delta(l, m) = 0
    For n = 1 To 2
    If (l = 1 And n = 2) = False Then
        delta(4 - l, m) = delta(4 - l, m) + w(4 - l + 1, n, m) * delta(4 - l + 1, n) * dReLU(z(4 - l, m))
    End If
    Next n
    Next m
    Next l
    
    '가중치 업데이트
    '(편미분을 하지 않고 역전파만으로 업데이트)
    For l = 1 To 4
    For n = 1 To 2
    If (l = 4 And n = 2) = False Then
        For m = 1 To 2
            w(l, n, m) = w(l, n, m) - L_rate * delta(l, n) * h(l - 1, m)
        Next m
        w(l, n, 3) = w(l, n, 3) - L_rate * delta(l, n)
    End If
    Next n
    Next l
    
Next i
Next k

'셀에 가중치 저장
For l = 1 To 4
For n = 1 To 2
For m = 1 To 3
If (l = 4 And n = 2) = False Then
    Worksheets("weightbias").Cells(n + (l - 1) * 2, m).Value = w(l, n, m)
End If
Next m
Next n
Next l

End Sub
{% endhighlight %}

<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드19.PNG">
</div>

{% highlight vb %}
Public Function gold(gold_input1, gold_input2)
    For l = 1 To 4
    For n = 1 To 2
    For m = 1 To 3
    If (l = 4 And n = 2) = False Then
        w(l, n, m) = Worksheets("weightbias").Cells(n + (l - 1) * 2, m).Value
    End If
    Next m
    Next n
    Next l

    x(1) = gold_input1
    x(2) = gold_input2
    h(0, 1) = x(1)
    h(0, 2) = x(2)
    
    For l = 1 To 4
    For n = 1 To 2
    If (l = 4) = False Then
        z(l, n) = 0
        For m = 1 To 2
            z(l, n) = z(l, n) + w(l, n, m) * h(l - 1, m)
        Next m
        z(l, n) = z(l, n) + w(l, n, 3)
        h(l, n) = ReLU(z(l, n))
    End If
    Next n
    Next l
    
    z(4, 1) = w(4, 1, 1) * h(3, 1) + w(4, 1, 2) * h(3, 2) + w(4, 1, 3)
    h(4, 1) = sigmoid(z(4, 1))
    y = h(4, 1)
    gold = y
End Function
{% endhighlight %}

<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드20.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드21.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드22.PNG">
</div>
<div style="align-items: center;">
    <img src="/assets/post-image/Excel-5일-단기-5/슬라이드23.PNG">
</div>

> 신경망이 깊어졌지만 오히려 코드는 더욱 간결해졌다. 위의 코드를 참고한다면 l, m, n에 값을 변경하여 원하는 크기의 신경망을 마음대로 구현할 수 있다. 엑셀로 구현하는 인공신경망에 있어서 이 코드에 나의 모든 노하우가 담겨있다고 봐도 무방하다..

<br>